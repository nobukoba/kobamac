macro main hid hid2 a=1 pow=0.0
  if [hid] = '' .or. [hid2] = '' .or. [a] = '' then
     message
     message please input HID newHID sigma const. !
     message
     exitm
  endif
  
*  hid2 = 101
*  while $hexist([hid2]) .eq. 1 do
*    hid2 = [hid2] + 1
*  endwhile

  xbins = $hinfo([hid],'xbins')
  xmax = $hinfo([hid],'xmax')
  xmin = $hinfo([hid],'xmin')

  y_fold  = $exec('mkvec [xbins] r')
  y_ufold = $exec('mkvec [xbins] r')
  y_gauss = $exec('mkvec [xbins] r')
  x_abs   = $exec('mkvec [xbins] r')
  x_shf   = $exec('mkvec [xbins] r')
  
  hi/get/con [hid] [y_ufold]
  hi/get/abs [hid] [x_abs]

  do i=1,[xbins]
    if [pow] .eq. 0 then
      sgm=[a]
    else
      sgm=$sigma([a]*[x_abs]([i])**([pow]))      
    endif
    sigma [x_shf]=[x_abs]-[x_abs](1)
    y_sum=$sigma(2.*vsum(exp(-[x_shf]**2/(2.*([sgm])**2)))-1.)
* mess [i],[sgm],[y_sum]
    sigma [x_shf]=[x_abs]-[x_abs]([i])
    sigma [y_gauss]=exp(-[x_shf]**2/(2.*([sgm])**2))/[y_sum]
    sigma [y_fold]=[y_fold]+[y_ufold]([i])*[y_gauss]
* vec/copy $sigma((1./(sqrt(2.*pi)*([sgm])))*exp(-[x_shf]**2/(2.*([sgm])**2))) [y_gauss]
  enddo

  title = $htitle([hid])
  1dhist [hid2] 'Fold.('//$QUOTE([title])//')' [xbins] [xmin] [xmax]
  hi/put/con [hid2] [y_fold]
* ht [hid2]
  
  exe dlvec [y_fold] [y_ufold] [y_gauss] [x_abs] [x_shf]
 
 return
 
 macro mkvec xbins=1 ri=''
   do i=1,9999
     vecname='temp'//[i]
     if $vexist([vecname]) .eq. 0 then
       breakl
     endif
   enddo
   vec/cre [vecname]([xbins]) [ri]
 return [vecname]
 
 macro dlvec
   do i=1,[#]
     vec/del [[i]]
   enddo  
 return
