macro tcal thresh=10 bin=4000 min=10 mm=1 max=4000 period=20 id
  v/del hist
  v/cre xx([bin]) R 
  sigma xx=array([bin],1#[bin])
  v/cre hist([bin]) R [bin]*0
  v/cre par(3) R
  v/cre imean([bin]) R
  v/cre itime([bin]) R
  sigma itime=xx*[period]
  hi/get_vect/content [id] hist
  i=[min]
  j=0
  
  zone 1 2
loop:
  if [i]<[max] then
    if hist([i])<[thresh] then
      i = [i]+1 
      goto loop
    endif
    imin=[i]

    while hist([i])>=[thresh] do
      i = [i]+1 
      if [i]>=[bin] then
        breakl
      endif
    endwhile
    imax=[i]

    if [imax]-[imin]>4 then
       v/fit xx([imin]:[imax]) hist([imin]:[imax]) ! g QN 0 par
    else
       sum =0
       sumx=0
       do k=[imin],[imax]
         w=hist([k]) 
         sum =[sum] +[w]
         sumx=[sumx]+[w]*[k]
*	 message k=[k],imin=[imin],imax=[imax],sum=[sum],sumx=[sumx]
       enddo
       if [sum]>0 then
         v/input par(2) $sigma([sumx]/[sum])
       else
*	 message sum=[sum],sumx=[sumx]
         v/input par(2) -1000
       endif
    endif
    v/print par(2)
    j = [j]+1
    v/input imean([j]) par(2)
    i = [i]+1 
    goto loop
  endif
  hi/pl [id]
  v/fit imean([mm]:[j]) itime([mm]:[j])  ! p1 ' *' 0 par
*  mess $sigma(par(2))
  v/write par(2) ! '(1x,f7.5)'
return

macro many thresh=10 bin=4000 min=10 mm=1 max=4000 period=20 from num=1
  zone 1 2
  vec/cre storep1([num]) R
  vec/cre storep2([num]) R
  vec/cre ids([num]) R
  do i=1,[num]
    id=[from]+[i]-1
    exec kumac/bbtcal2#tcal [thresh] [bin] [min] [mm] [max] [period] [id]
    vec/inp ids([i]) [id]
    vec/inp storep1([i]) par(2)
    vec/inp storep2([i]) par(3)
    wait
  enddo
  vec/write ids,storep1,storep2 ! '(f5,1x,f7.5,1x,f20.15)'
*  vec/write ids,store, ./csi-tcal.txt '(f5,1x,f7.5)'
return

