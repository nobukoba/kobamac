macro main hidstr fit_func=p1 vx1 vx2 vx3 vx4
   ind = $index([hidstr],'(') - 1
   hid = $substring([hidstr],1,[ind])
   hi/pl [hidstr]
   
   if ($vexist(vx)) then
     vec/del vx
   endif
   if ($vexist(vy)) then
     vec/del vy
   endif
   while ([vx4] .eq. '') do
     mess 'Click 4 points'
     vlocate vx vy
     np = $vlen(vx,1)
     if ([np] .ge. 4) then
       do i = 1,4
	 vx[i]= vx([i])
	 vy[i]= vy([i])
       enddo
     endif
     vec/del vx,vy
   endwhile
   
   xbins = $hinfo([hid],'xbins')
   xmax = $hinfo([hid],'xmax')
   xmin = $hinfo([hid],'xmin')
   dx   = (([xmax]) - ([xmin]))/([xbins])
   vec/cre vhxx1([xbins]) r
   vec/cre vhyy1([xbins]) r
   vec/cre vhye1([xbins]) r
   vec/cre vhxx2([xbins]) r
   vec/cre vhyy2([xbins]) r
   vec/cre vhye2([xbins]) r
   vec/cre vhxx3([xbins]) r
   vec/cre vhyy3([xbins]) r
   vec/cre vhye3([xbins]) r
   get/abs [hid] vhxx1
   get/con [hid] vhyy1
   get/err [hid] vhye1
   
   k = 1
   l = 1
   do i = 1, [xbins]
     xxtmp = vhxx1([i])
     yytmp = vhyy1([i])
     yetmp = vhye1([i])
     if ((([xxtmp] .ge. [vx1]) .and. ([xxtmp] .le. [vx2]))_
       .or. (([xxtmp] .ge. [vx3]) .and. ([xxtmp] .le. [vx4]))) then
       vec/input vhxx2([k])  [xxtmp]
       vec/input vhyy2([k])  [yytmp]
       vec/input vhye2([k])  [yetmp]
       k = [k] + 1
     endif
     if (([xxtmp] .ge. [vx2]) .and. ([xxtmp] .le. [vx3])) then
       vec/input vhxx3([l])  [xxtmp]
       vec/input vhyy3([l])  [yytmp]
       vec/input vhye3([l])  [yetmp]
       l = [l] + 1
     endif
   enddo   
   vsize2 = [k] - 1
   vsize3 = [l] - 1
   
   vec/copy vhxx2(1:[vsize2]) vhxx4
   vec/copy vhyy2(1:[vsize2]) vhyy4
   vec/copy vhye2(1:[vsize2]) vhye4
   vec/copy vhxx3(1:[vsize3]) vhxx5
   vec/copy vhyy3(1:[vsize3]) vhyy5
   vec/copy vhye3(1:[vsize3]) vhye5
   
   cur_plci = $GRAFINFO('PLCI')
   set plci 1
   exe draw_line_x [vx1] [vx2] [vx3] [vx4]
   set plci [cur_plci]
   
   if ([fit_func] .eq. 'e') then
     yi = $sigma(log(vhyy4(1)))
     yf = $sigma(log(vhyy4([vsize2])))
   endif
   if ([fit_func] .eq. 'p1') then
     yi = $sigma(vhyy4(1))
     yf = $sigma(vhyy4([vsize2]))
   endif
   xi = $sigma(vhxx4(1))
   xf = $sigma(vhxx4([vsize2]))
   p1 = (([xi])*([yf])-([xf])*([yi]))/(([xi])-([xf]))
   p2 = (([yi])-([yf]))/(([xi])-([xf]))
   vec/cre par(2) r [p1] [p2]
   cur_plci = $GRAFINFO('PLCI')
   set plci 2
   vec/fit vhxx4 vhyy4 vhye4 [fit_func] s 2 par
   par1 = $sigma(par(1))
   par2 = $sigma(par(2))
   if ([fit_func] .eq. 'e') then
     sigma vfunc  = exp(([par1]) + ([par2])*vhxx5)
   endif
   if ([fit_func] .eq. 'p1') then
     sigma vfunc  = ([par1]) + ([par2])*vhxx5
   endif
   sigma vsub = vhyy5 - vfunc
   gra [vsize3] vhxx5 vsub l
   set plci [cur_plci]
   integ = $sigma(vsum(vsub))
   area  = [dx]*[integ]
   mess counts: [integ],  area: [area]
   
   vec/del vhxx1,vhyy1,vhye1
   vec/del vhxx2,vhyy2,vhye2
   vec/del vhxx3,vhyy3,vhye3
   vec/del vhxx4,vhyy4,vhye4
   vec/del vhxx5,vhyy5,vhye5
   vec/del par,vfunc,vsub
 return
 
 macro draw_line_x
   ymin = $GRAFINFO('WNYMIN')
   ymax = $GRAFINFO('WNYMAX')
   if $OPTION('LOGY') = 1 then
     ymin = $sigma(10**([ymin]))
     ymax = $sigma(10**([ymax]))
   endif
   do i = 1,[#]
     dline [[i]] [[i]] [ymin] [ymax]
   enddo
 return
